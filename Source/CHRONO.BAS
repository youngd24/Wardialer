100 REM *****************************************************
110 REM APPLE II CODE TO COMMUNICATE WITH A HAYES CHRONOGRAPH
120 REM TAKEN FROM THE MANUAL
130 REM *****************************************************
140 GOSUB 1000:REM INITIALIZE PROGRAM
150 OST$="ATVT:": GOSUB 2000: REM SET TIME SEPARATOR
160 OST$="ATVD/": GOSUB 2000: REM SET DATE SEPARATOR
170 OST$="ATRT": GOSUB 2000: $=IST$: REM READ TIME
180 OST$="ATRD": GOSUB 2000: DTE$=IST$: REM READ DATE
190 OST$="ATRW": GOSUB 2000: W=VAL(IST$): REM READ WEEKDAY
200 GOSUB 3000: REM CONVERT WEEKDAY CODE TO CHARACTERS
210 GOSUB 4000: REM OUTPUT TIME, WEEKDAY, AND DATE ON SCREEN
220 END

1000 CS=7: REM COMMUNICATIONS CARD SLOT
1010 CR$=CHR$(13): REM CARRIAGE RETURN
1020 D$=CR$+CHR$(4): REM DOS 3.3 CONTROL-D
1030 RETURN

2000 IST$="": REM CLEAR INPUT STRING
2010 PRINT D$"PR#"CS: REM OUTPUT TO CLOCK ON
2020 PRINT OST$: REM OUTPUT COMMAND
2030 PRINT D$"PR#0": REM OUTPUT TO CLOCK OFF
2040 PRINT D$"IN#"CS: REM INPUT FROM CLOCK ON
2050 GET X$: REM GET ONE CHARACTER FROM CLOCK
2060 IF X$=CR$ THEN 2090
2070 IST$=IST$+X$
2080 GTO 2050
2090 PRINT D$"IN#0": REM INPUT FROM CLOCK ON
2100 RETURN

3000 FOR LOOP=0 TO W
3010 READ DAY$
3020 NEXT LOOP
3030 RETURN
3040 DATA MONDAY, TUESDAY, WEDNESDAY
3050 DATA THURSDAY, FRIDAY, SATURDAY
3060 DATA SUNDAY

4000 PRINT "TIME IS ";TIM$
4010 PRINT "TODAY IS ";DAY$" ";DTE$
4020 RETURN