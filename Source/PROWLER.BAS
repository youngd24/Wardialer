10 REM %%%%%%%%%%%%%%%%%%%%%%%%THE PREFIX PROWLER 1.2
20 REM %%%%%%%%%%%%%%%%%%%%%%%% WRITTEN BY: A U T O M A N%%%%%%%%%%%%%%%%%%%%%%%%
30 REM
40 REM LIST THIS IN 80 COLUMNS
50 LOMEM: 24576
60 DIM RN(100)
70 HM = PEEK(175) + PEEK(176) *256 -52
80 D$ = CHR$(13) + CHR$(4)
90 DIM SN$(200)
100 V = 4:H = 1
110 CALL HM: GOSUB 2760
120 T = 15: SPEED= 220: HOME : VTAB 6: HTAB 6:A$ = "AUTO'S AUTOMATIC WAR DIALER:": GOSUB 2570: VTAB 8: HTAB 11:A$ = "THE PREFIX PROWLER": GOSUB 2570
130 VTAB 10: HTAB 15:A$ = "WRITTEN BY:": GOSUB 2570: VTAB 12: HTAB 13:A$ = "A U T O M A N": GOSUB 2570: VTAB 13: HTAB 13:A$ = "~~~~~~~~~~~~~": GOSUB 2570
140 VTAB 15: HTAB 15:A$ = "PART I OF:": GOSUB 2570: VTAB 17: HTAB 10:A$ = "THE HAYES HACKAMATIC": GOSUB 2570
150 SPEED= 255:T = 4: FOR I = 17 TO 14 STEP -1: VTAB I: GOSUB 2510: NEXT
160 SPEED= 200:M = 1: FOR X = 29 TO 12 STEP -1: HTAB X: PRINT MID$ (C$,M,1);" ";
170 M = M +1: IF M = 5 THEN M = 1
180 NEXT :T = 15: SPEED= 255
190 SPEED= 255: VTAB 13: HTAB 12: FOR I = 1 TO 15: GOSUB 2510: NEXT
200 SPEED= 255: CALL HM
210 REM DEFAULTS
220 SL$ = "2": REM SLOT
230 PR$ = "N": REM PROMOTHEUS
240 EX$ = "N": REM EXTENDER
250 EA$ = "":LD$ = "":D1 = 5:D2 = 2
260 WA$ = "": REM WAR-AREA CODE
270 SN$ = "": REM STARTING NUMBER
280 ES$ = "9999": REM ENDING SUFFIX
290 D% = 1
300 I$ = "1":INC = 1:BL$ = "20"
310 DL$ = "10":TS$ = "60":RD$ = "3": REM DELAY B/F DISCONNECT, TOUCH-TONE SPEED, # OF REDIALS
315 SP$ = "Y": REM DEFAULT SPEAKER STATUS
320 TEXT : HOME : INVERSE : PRINT SPC( 11);"THE PREFIX PROWLER"; TAB( 41)
330 VTAB 2: PRINT TAB( 14);"LOAD DATA FILE"; TAB( 41): NORMAL : POKE 34,1
340 REM DRIVE SELECT
350 VTAB 6: HTAB 12 +11 *(D% = 2): INVERSE : PRINT "DRIVE ";D%;: NORMAL : HTAB 23 -11 *(D% = 2): NORMAL : PRINT "DRIVE ";D% -(D% = 2) +(D% = 1);
360 WAIT -16384,128: GET A$: IF A$ = CHR$(13) THEN 380
365 IF A$ = CHR$(27) THEN 2170
370 D% = 1 +(D% = 1): GOTO 350
380 VTAB 12: CALL -958: HTAB 11: PRINT "ENTER DATA FILE NAME:";: VTAB 13: HTAB 13: PRINT "(.PP IS APPENDED)": VTAB 15: HTAB 10:LN = 20:LE = 1:DE$ = DF$: GOSUB 3010:DF$ = NU$
390 IF DF$ = CHR$(27) THEN DF$ = "": VTAB 3: CALL -958: GOTO 350
400 IF DF$ = "" THEN 510
410 ONERR GOTO 450
420 PRINT D$;"UNLOCK";DF$".PP"",D"D%
430 GOSUB 2280
440 GOTO 510
450 IF PEEK(222) = 6 THEN VTAB 17: HTAB 5: PRINT "FILE NOT FOUND...CREATE? ";: GET A$: IF A$ < >"Y" THEN 380
460 IF PEEK(222) < >6 THEN VTAB 19: HTAB 16: PRINT "ERROR #"; PEEK(222): HTAB 13: PRINT "PRESS ANY KEY: ";: GET A$: GOTO 380
470 PRINT D$;"OPEN";DF$".PP"
480 PRINT D$;"WRITE";DF$".PP"
490 PRINT 0
500 PRINT D$;"CLOSE"
510 IF SL$ = "0" THEN SL$ = ""
520 POKE 216,0: HOME : INVERSE : VTAB 2: PRINT SPC( 12);"MODEM PARAMETERS"; SPC( 12);: NORMAL : PRINT : POKE 34,2
530 HTAB 13: VTAB 4: PRINT "SLOT OF MODEM?->";:DE$ = SL$:LN = 1:LE = 1: GOSUB 3010: IF NU$ = CHR$(27) THEN 320
540 IF VAL(NU$) <1 OR VAL(NU$) >7 THEN PRINT "": GOTO 510
545 SL$ = NU$:SL = VAL(SL$)
550 VTAB 5: HTAB 3: PRINT "USE PROMETHEUS PROMODEM?->";:DE$ = PR$:LN = 1:LE = 1: GOSUB 3010: IF NU$ < >"Y" AND NU$ < >"N" THEN PRINT "";: GOTO 550
560 PR$ = NU$
570 VTAB 6: HTAB 5: PRINT "USE AN EXTENDER? ->";:DE$ = EX$:LE = 1:LN = 1: GOSUB 3010:EX$ = NU$: IF EX$ < >"Y" AND EX$ < >"N" THEN PRINT "";: GOTO 570
580 IF EX$ = "N" THEN 650
590 VTAB 7: HTAB 12: PRINT " EXT. AREA CODE->";:DE$ = EA$:LN = 4:LE = 1: GOSUB 3010:EA$ = NU$: IF EA$ = "" THEN HTAB 30: PRINT "LOCAL ": GOTO 610
600 IF VAL(EA$) <100 OR VAL(EA$) >1999 THEN EA$ = "": PRINT "";: GOTO 590
610 VTAB 8: HTAB 2: PRINT "LONG DIST. SERVICE NUMBER-> [XXX-XXXX]";: HTAB 31:N$ = LD$:LN = 7: GOSUB 2870:LD$ = N$
620 HTAB 4: VTAB 10: PRINT "ACCESS CODE: ";:LN = 20:DE$ = CO$: GOSUB 3010:CO$ = NU$
625 IF PR$ = "Y" THEN 640
630 VTAB 12: HTAB 2: PRINT "DELAY BEFORE DIALING CODE-> SEC.";: HTAB 29:LN = 2:DE$ = STR$(D1): GOSUB 3010: IF VAL(NU$) <1 THEN PRINT "": GOTO 630
635 D1 = VAL(NU$)
640 VTAB 13: HTAB 11: PRINT "DELAY AFTER CODE-> SEC.";:DE$ = STR$(D2):LN = 2: HTAB 29: GOSUB 3010:D2 = VAL(NU$)
650 PRINT : PRINT
660 HTAB 7: PRINT "WARDIALING AREA CODE-> ";:DE$ = WA$:LN = 4:LE = 1: GOSUB 3010:WA$ = NU$: IF WA$ = "" THEN HTAB 30: PRINT "LOCAL";: GOTO 680
670 IF VAL(WA$) <100 OR VAL(WA$) >1999 THEN WA$ = "": PRINT "";: GOTO 660
680 IF LEN(AC$) >4 THEN PRINT "": GOTO 660
690 PRINT : HTAB 12: PRINT "STARTING NUMBER-> [XXX-XXXX]";: HTAB 31
700 N$ = SN$
710 GOSUB 2870
720 IF SN$ < >"" AND N$ < >SN$ THEN RM = 0
730 SN$ = N$
740 PF$ = LEFT$(N$,3):SS = VAL( RIGHT$(N$,4))
750 PRINT
760 HTAB 14: PRINT "ENDING NUMBER-> [";PF$;"- ]";: HTAB 35:S = 1:N$ = ES$: GOSUB 2870
770 ES$ = N$ : ES = VAL(N$) : IF ES < SS OR ES > 9999 THEN GOTO 760
780 PRINT : HTAB 6: CALL -958: PRINT "INCREMENT OR [R]ANDOM->";:DE$ = I$:LN = 2:LE = 1: GOSUB 3010:I$ = NU$
790 IF I$ = "R" THEN DR = 1: GOTO 820
800 INC = VAL(I$): IF INC <1 THEN PRINT "";: GOTO 780
810 GOTO 880
820 PRINT : PRINT : HTAB 7: INVERSE : PRINT "GENERATING RANDOM NUMBER SEED": NORMAL : HTAB 15: PRINT "PRESS ANY KEY"
830 IF PEEK( -16384) <128 THEN ZQ = RND(1): GOTO 830
840 POKE -16368,0
850 HTAB 1: CALL -868: PRINT " ENTER RANDOM DIALING BLOCK LENGTH: ";:DE$ = BL$:LN = 2: GOSUB 3010: IF NU$ < >BL$ THEN RM = 0
860 BL$ = NU$:BL = VAL(BL$): IF BL <1 OR BL >99 THEN PRINT "";: GOTO 850
870 INC = BL
880 PRINT : HTAB 12: PRINT "CORRECT? Y";: CALL -1008: GET CH$
890 IF CH$ = "N" THEN 510
900 HOME
910 VTAB 5: HTAB 3: PRINT "TIME TO WAIT FOR CARRIER": HTAB 15: PRINT "(1-255 SEC.)->";:DE$ = DL$:LE = 1:LN = 3: GOSUB 3010: IF NU$ = CHR$(27) THEN 510
920 IF VAL(NU$) <1 OR VAL(NU$) >255 THEN PRINT "": GOTO 910
925 DL$ = NU$:WC = VAL(DL$)
930 VTAB 8: HTAB 9: PRINT "TOUCH TONE SPACING": VTAB 9: HTAB 9: PRINT "(30-255 MILLISEC.)->";:DE$ = TS$:LN = 3: GOSUB 3010
940 IF VAL(NU$) <30 OR VAL(NU$) >255 THEN PRINT "": GOTO 930
945 TS$ = NU$:TS = VAL(TS$)
950 IF PR$ = "N" THEN 985
960 VTAB 11: HTAB 12: PRINT "REDIALS ON BUSY->";:DE$ = RD$:LN = 2:LE = 1: GOSUB 3010:RD$ = NU$: IF RD$ = "" THEN HTAB 30: PRINT "NO LIMIT";: GOTO 990
970 IF VAL(RD$) <0 THEN PRINT "";:RD$ = "": GOTO 960
980 RD = VAL(RD$)
985 PRINT : PRINT
987 HTAB 16: PRINT "SPEAKER ON?-> ";:DE$ = SP$:LN = 1:LE = 1: GOSUB 3010: IF NU$ < >"Y" AND NU$ < >"N" THEN PRINT "";: GOTO 987
988 SP$ = NU$
990 PRINT : PRINT : HTAB 12: PRINT "CORRECT? Y";: CALL -1008: GET CH$
1000 IF CH$ = "N" THEN 900
1010 HTAB 1: FOR ZQ = 1 TO 2: VTAB ZQ: CALL -868: NEXT : CALL HM: TEXT : HOME
1020 VTAB 20: FOR I = 1 TO 40: PRINT "-";: NEXT I
1030 VTAB 21: HTAB 7: INVERSE : PRINT "N";: NORMAL : PRINT ": NUMBER SKIPPED BEFORE HANGUP": INVERSE : PRINT "INVERSE";: NORMAL : PRINT ": BUSY - ABORTED BEFORE ANSWER": FLASH : HTAB 3: PRINT "FLASH";: NORMAL : PRINT ": SUCCESSFUL CONNECT"
1040 VTAB 3: FOR I = 1 TO 40: PRINT "-";: NEXT I
1050 POKE 35,2
1060 HOME
1070 PR# SL: SPEED= 100
1080 PRINT "ATZ": GOSUB 1950
1090 REM INITIALIZE MODEM
1100 REM NOCEHO
1110 PRINT "ATE0"
1120 GOSUB 1940
1130 REM NUMERIC RESULT CODES
1140 IN# SL
1150 PRINT "ATV";2 *(PR$ = "Y")
1160 GET A$
1170 GOSUB 1940
1175 REM NO ANSWER
1177 PRINT "ATS0=0": GET A$: GOSUB 1940
1180 REM DELAY BEFORE HANGUP
1190 PRINT "ATS7=";DL$
1200 GET A$
1210 GOSUB 1940
1220 REM DIALING SPEED
1230 PRINT "ATS11=";TS$
1240 GET A$
1250 GOSUB 1940
1260 REM RESPONSE TIME, CARRIER DETECT
1270 PRINT "ATS9=4"
1280 GET A$
1290 GOSUB 1940
1340 REM COMMA DELAY LENGTH
1350 PRINT "ATS8=";D2: GET A$: GOSUB 1940
1360 REM ESCAPE CODE DEAD TIME
1370 PRINT "ATS12=25": GET A$: GOSUB 1940
1375 REM SPEAKER ON/OFF
1377 PRINT "ATM";(SP$ = "Y"): GET A$: GOSUB 1940
1380 IN# 0: SPEED= 255
1390 REM DIALING LOOP
1400 FOR X = SS TO ES STEP INC
1410 IF DR AND RM THEN FOR RN = 1 TO RM
1420 IF DR AND NOT RM THEN BL% = BL *((ES -X >BL)) +(ES -X +1) *((ES -X < = BL)): GOSUB 2650: FOR RN = 1 TO BL%
1430 PR# 0
1440 NUM$ = PF$
1450 NUM$ = NUM$ +"-"
1460 IF DR THEN N = X +RN(RN) -1: GOTO 1480
1470 N = X
1480 FOR I = 1 TO 3: IF N < INT(10 ^I) THEN NUM$ = NUM$ +"0"
1490 NEXT I
1500 NUM$ = NUM$ + STR$(N)
1510 SPEED= 255: VTAB V: HTAB H: PRINT NUM$
1520 VTAB 1
1530 HOME
1540 MN$ = ""
1550 MN$ = WA$ + LEFT$(NU$,3) + RIGHT$(NU$,4)
1560 GOSUB 1940: PR# SL: SPEED= 150
1570 IF EX$ = "Y" THEN 1600
1580 PRINT "ATDT";MN$
1590 GOTO 1660
1600 PRINT "ATDT";
1610 PRINT EA$; LEFT$(LD$,3); RIGHT$(LD$,4);
1620 IF PR$ = "Y" THEN PRINT "W DT";CO$;",";MN$: GOTO 1660
1630 PRINT ";"
1640 TI% = D1: GOSUB 2830
1650 SPEED= 150: PRINT "ATDT";CO$;",";MN$
1660 SPEED= 255: GOSUB 1940
1670 REM INPUT LOOP
1680 PR# 0: IN# SL
1690 IF PR$ = "N" THEN PRINT "?";: GET R$: PRINT R$
1692 IF PR$ = "Y" THEN INPUT R$
1695 R = VAL(R$)
1700 REM CONNECT
1710 IF R = 1 OR R = 5 THEN SN$(C +1) = NUM$:C = C +1: VTAB 1: GOSUB 2190: VTAB V: HTAB H: FLASH : PRINT NUM$: NORMAL :SN$ = NUM$: GOSUB 2380: GOTO 1840
1720 REM NO CARRIER
1730 IF R = 3 THEN 1840
1740 IF R$ = "Q" THEN 1970
1750 IF R$ = "G" THEN POKE -16297,0: POKE -16304,0: POKE -16299,0: POKE -16302,0: GOTO 1690
1760 IF R$ = "T" THEN POKE -16303,0: GOTO 1690
1770 IF R$ = "N" THEN VTAB 1: GOSUB 2190: VTAB V: HTAB H: PRINT NUM$;: INVERSE : PRINT "N": NORMAL : GOTO 1840
1780 REM BUSY
1790 IF R = 62 OR R = 63 THEN BUSY = BUSY +1
1800 IF R = 43 OR R = 44 OR R$ = "R" THEN GOSUB 2190: GOTO 1560
1810 IF R = 62 AND RD$ = "" THEN 1830
1820 IF BUSY >RD THEN VTAB 1: GOSUB 2190: VTAB V: HTAB H: INVERSE : PRINT NUM$: NORMAL : GOTO 1840
1830 GOTO 1690
1840 BUSY = 0:H = H +10: IF H >32 THEN H = 1:V = V +1
1850 IF V >19 THEN POKE 35,19: POKE 34,3: CALL -912: POKE 34,0: POKE 35,2:V = 19
1860 IN# 0
1870 GOSUB 1950
1880 SN$ = NUM$:NUM$ = ""
1890 VTAB 1
1900 IF DR THEN NEXT RN
1910 RM = 0
1920 NEXT X
1930 GOTO 1970
1940 REM DELAY
1950 FOR P = 1 TO 1000: NEXT
1960 RETURN
1970 REM QUIT AND DISPLAY NUMBERS
1980 IF NOT DR THEN 2040
1990 SN$ = PF$ +"-":ZQ = X: FOR I = 1 TO 3: IF ZQ < INT(10 ^I) THEN SN$ = SN$ +"0"
2000 NEXT I
2010 SN$ = SN$ + STR$(ZQ)
2020 IF DR AND RM THEN RM% = RM -RN +1
2030 IF DR AND NOT RM THEN RM% = BL% -RN +1: IF RM% <0 THEN RM% = 0
2040 GOSUB 2190
2050 TEXT : VTAB 21: CALL -958: CALL HM: HOME
2060 GOSUB 2380
2070 IF NOT DR THEN HTAB 6: PRINT "LAST NUMBER DIALED: ";
2080 IF DR THEN PRINT "FIRST NUMBER": HTAB 3: PRINT "IN CURRENT RANDOM BLOCK: ";
2090 PRINT SN$
2100 IF DR THEN PRINT : PRINT "NUMBERS REMAINING IN CURRENT BLOCK: ";RM%
2110 PRINT : PRINT
2120 IF C = 0 THEN HTAB 11: PRINT "NO COMPUTERS FOUND": GOTO 2170
2130 HTAB 12: PRINT "COMPUTERS FOUND:": PRINT
2140 FOR I = 1 TO C
2150 PRINT TAB( 10);I;"."; TAB( 20);SN$(I)
2160 NEXT
2170 PRINT : PRINT : HTAB 9: PRINT "QUIT THE PROWLER? (Y/N) ";: WAIT -16384,128:A$ = CHR$( PEEK( -16384) -128): IF A$ < >"Y" THEN POKE -16368,0: CALL 1002: RUN
2180 ONERR GOTO 2185
2182 CALL 1002: PRINT D$;"BRUNHACK HELLO,D1"
2185 TEXT : HOME : POKE -16368,0: END
2190 REM HANG UP
2200 IN# 0
2210 PR# SL
2220 SPEED= 100
2230 PRINT "A": FOR ZQ = 1 TO 5: GOSUB 1940: NEXT ZQ: PRINT "+++";: FOR ZQ = 1 TO 5: GOSUB 1940: NEXT ZQ
2240 PRINT "ATH"
2250 GOSUB 1940
2260 PR# 0
2270 SPEED= 255: RETURN
2280 REM READ FILE DATA
2290 PRINT D$;"OPEN "DF$".PP"
2300 PRINT D$;"READ "DF$".PP"
2310 INPUT SL$: IF SL$ = "0" THEN 2360
2320 INPUT PR$: INPUT EX$: IF EX$ = "Y" THEN INPUT EA$: INPUT LD$: INPUT D1: INPUT D2:
2330 INPUT WA$: INPUT SN$: INPUT ES$: INPUT I$: INPUT INC: IF I$ = "R" THEN INPUT BL$
2340 INPUT DL$: INPUT TS$: IF PR$ = "Y" THEN INPUT RD$
2350 INPUT N: FOR R = 1 TO N: INPUT SN$(R): NEXT : IF I$ = "R" THEN INPUT RM: FOR R = 1 TO RM: INPUT RN(R): NEXT
2360 PRINT D$;"CLOSE"
2370 RETURN
2380 REM PUT FILE
2390 POKE 216,0
2400 IF DF$ = "" THEN 2500
2410 CALL 1002
2420 PRINT D$;"DELETE "DF$".PP"
2430 PRINT D$;"OPEN "DF$".PP"
2440 PRINT D$;"WRITE "DF$".PP"
2450 PRINT SL$: PRINT PR$: PRINT EX$: IF EX$ = "Y" THEN PRINT EA$: PRINT LD$: PRINT D1: PRINT D2
2460 PRINT WA$: PRINT SN$: PRINT ES$: PRINT I$: PRINT INC: IF I$ = "R" THEN PRINT BL$
2470 PRINT DL$: PRINT TS$: IF PR$ = "Y" THEN PRINT RD$
2480 PRINT C: FOR R = 1 TO C: PRINT SN$(R): NEXT R: IF I$ = "R" THEN PRINT RM%: FOR R = RN TO RN +RM% -1: PRINT RN(R): NEXT R
2490 PRINT D$;"CLOSE"
2500 RETURN
2510 REM CURSOR
2520 C$ = "!/-\":M = 1: FOR R = 1 TO T: PRINT MID$ (C$,M,1); CHR$(8);:M = M +1
2530 IF M = 5 THEN M = 1
2540 NEXT
2550 PRINT " ";
2560 RETURN
2570 REM CUTEPRINT
2580 GOSUB 2510
2590 IF PEEK( -16384) >128 THEN POKE -16368,0: GOTO 200
2600 M = 1: FOR R = 1 TO LEN(A$): PRINT MID$ (A$,R,1); MID$ (C$,M,1); CHR$(8);
2610 IF M = 5 THEN M = 1
2620 NEXT
2630 GOSUB 2510
2640 RETURN
2650 REM RANDOM NUMBER TABLE
2660 IF RM THEN RETURN
2670 RR$ = BH$
2680 VTAB 1: HTAB 26: INVERSE : PRINT "SHUFFLING:": NORMAL
2690 FOR Z1 = BL% TO 1 STEP -1
2700 VTAB 1: HTAB 38: PRINT Z1;" ";
2710 TMP = INT( RND(1) *Z1) *2 +1
2720 RN(Z1) = VAL( MID$ (RR$,TMP,2))
2730 RR$ = MID$ (RR$,1,TMP -1) + MID$ (RR$,TMP +2)
2740 NEXT Z1
2750 RETURN
2760 REM GENERATE RANDOM MASKING TAPE
2770 RR$ = ""
2780 FOR Z0 = 1 TO 99
2790 IF Z0 <10 THEN BH$ = BH$ +"0"
2800 BH$ = BH$ + STR$(Z0)
2810 NEXT Z0
2820 RETURN
2830 REM DELAY2
2840 V0 = PEEK(37) +1:H0 = PEEK(36) +1: SPEED= 255: PR# 0
2850 FOR P = TI% TO 0 STEP -1: VTAB 1: HTAB 28: PRINT "WAITING...";P;" ";: FOR P2 = 1 TO 500: NEXT P2,P
2860 PR# SL: HTAB H0: VTAB V0: RETURN
2870 REM INPUT A PHONE NUMBER
2880 PRINT N$;
2890 IF NOT S AND LEN(N$) = 3 THEN PRINT "-";:N$ = N$ +"-"
2900 IF A AND LEN(N$) = 7 THEN PRINT "-";:N$ = N$ +"-"
2910 WAIT -16384,128: GET DI$
2920 IF DI$ < > CHR$(8) THEN 2980
2930 IF LEN(N$) = 4 AND NOT S THEN PRINT CHR$(8);:N$ = LEFT$(N$,3)
2940 IF LEN(N$) = 8 AND A = 1 THEN PRINT CHR$(8);:N$ = LEFT$(N$,7)
2950 IF LEN(N$) >0 THEN PRINT "";
2960 IF LEN(N$) >1 THEN N$ = LEFT$(N$, LEN(N$) -1): GOTO 2910
2970 N$ = "": GOTO 2910
2980 IF ( ASC(DI$) > = 48) AND ( ASC(DI$) < = 57) AND ( LEN(N$) <8 +4 *A -4 *S) THEN PRINT DI$;:N$ = N$ +DI$: GOTO 2890
2990 IF DI$ = CHR$(13) AND LEN(N$) = 8 +4 *A -4 *S THEN S = 0: RETURN
3000 PRINT "";: GOTO 2910
3010 REM INPUT CHAR STRING, MAX LENGTH=LN
3020 REM PASS IN: DEFAULT=DE$, LE=1 (IF LETTERS ALLOWED)
3030 REM RETURNS: NU$=NUMBER
3040 L0 = LN
3050 NU$ = ""
3060 RX = PEEK(36) +1
3070 PRINT "[";
3080 IF DE$ = "" THEN FOR R = 1 TO LN: PRINT " ";: NEXT : PRINT "]";: HTAB RX +1: GOTO 3160
3090 FOR R = 1 TO LEN(DE$):T$ = MID$ (DE$,R,1): IF ASC(T$) <32 THEN PRINT "^"; CHR$( ASC(T$) +64);:LN = LN -1: GOTO 3110
3100 PRINT T$;
3110 NEXT
3120 IF LEN(DE$) = LN THEN PRINT "]";: GOTO 3140
3130 FOR R = 1 TO LN - LEN(DE$): PRINT " ";: NEXT : PRINT "]";
3140 FOR R = 1 TO LN +1 - LEN(DE$): CALL -1008: NEXT :NU$ = DE$
3150 IF LEFT$(DE$,1) = CHR$(16) THEN LN = LN -3
3160 GET A$
3170 IF A$ = "" THEN 3160
3180 IF A$ < > CHR$(13) AND NOT RF THEN RF = 1
3190 IF A$ = CHR$(13) THEN 3330
3200 IF A$ < > CHR$(8) THEN 3250
3210 IF LEN(NU$) >0 THEN IF ASC( RIGHT$(NU$,1)) <32 THEN PRINT "";:LN = LN +1
3220 IF LEN(NU$) >1 THEN PRINT "";:NU$ = LEFT$(NU$, LEN(NU$) -1): GOTO 3160
3230 IF LEN(NU$) = 1 THEN PRINT "";:NU$ = "": GOTO 3160
3240 PRINT "";: GOTO 3160
3250 IF NOT LE AND (A$ <"0" OR A$ >"9") THEN PRINT "";: GOTO 3160
3260 IF ASC(A$) = 27 AND LE = 1 THEN NU$ = A$: GOTO 3360
3270 IF NOT LE AND ASC(A$) <32 THEN PRINT "";: GOTO 3160
3280 IF LEN(NU$) >LN -1 THEN PRINT "";: GOTO 3160
3290 IF ASC(A$) <32 THEN PRINT "";: GOTO 3160
3300 PRINT A$;
3310 NU$ = NU$ +A$: IF ASC(A$) = 16 AND LEFT$(NU$,1) = A$ THEN LN = LN -3
3320 GOTO 3160
3330 IF LEN(NU$) = 0 AND DE$ = "" AND LE = 0 THEN PRINT "";: GOTO 3160
3340 IF NOT RF THEN NU$ = DE$: GOTO 3360
3350 PRINT SPC( LN - LEN(NU$));
3360 HTAB RX: PRINT " ";: HTAB RX +L0 +1: PRINT " ";:N$ = "":DE$ = "":DE = 0:LN = 0:L0 = 0:A = 0:LE = 0: RETURN